apply plugin: 'osgi'
apply plugin: 'signing'
apply plugin: 'maven-publish'

dependencies {
  compile project(":reveno-core")

  compile "org.jgroups:jgroups:$jgroupsVersion"
  compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

  compile "de.ruedigermoeller:fast-cast:$fastcastVersion"

  testCompile project(":reveno-test-model")
}

test {
  maxParallelForks = 1
  minHeapSize = "256m"
  maxHeapSize = "512m"
  forkEvery = 1
}

jar {
  manifest {
    name = 'org.reveno.reveno-core'
    instruction 'Bundle-Vendor', 'Reveno'
    instruction 'Bundle-Description', 'Reveno Clustering'
    instruction 'Bundle-DocURL', 'http://reveno.org'
  }
}

task fooJar(type: Jar) {
  manifest = osgiManifest {
    instruction 'Bundle-Vendor', 'Reveno'
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

signing {
  sign configurations.archives
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'org.reveno'
      artifactId 'reveno-cluster'
      version "$revenoVersion"
      from components.java

      artifact sourceJar {
        classifier "sources"
      }
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Reveno Cluster'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Reveno component wich makes it possible to use engine in cluster environment.'
        url 'http://reveno.org'

        scm {
          connection 'scm:git:https://github.com/dmart28/reveno.git'
          developerConnection 'scm:git:git@github.com:dmart28/reveno.git'
          url 'https://github.com/dmart28/reveno'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'dmart28'
            name 'Artem Dmitriev'
            email 'artem@reveno.org'
          }
        }
      }
    }
  }
}